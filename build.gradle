plugins {
    id 'me.champeau.gradle.jmh' version '0.5.2' apply false
    id 'com.github.ben-manes.versions' version '0.27.0' apply false
    id 'org.owasp.dependencycheck' version '6.0.5' apply false
    id "com.github.spotbugs" version "4.7.2" apply false
    id 'io.morethan.jmhreport' version '0.9.0' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'maven'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    ext {
        javaJwtVersion = '3.12.0'
        jwksRsaVersion = '0.15.0'
        jjwtVersion = '0.9.1'
        nimbusVersion = '9.4.1'
        jmhVersion = '1.27'
        oktaVersion = '0.5.0'
        fusionAuthVersion = '4.0.1'
        junitJupiterVersion = '5.7.0'
        jaxbVersion = '2.3.1'
    }
    
    repositories {
        mavenLocal()
        jcenter()
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }

        reports {
            html.enabled = true
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    }

    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.matching { task -> task.name.startsWith('spotbugs') }.forEach {
        it.reports {
            html.enabled = true
            xml.enabled = false
            // spotbugs does not allow to generate a xml and html report at once https://github.com/spotbugs/spotbugs/issues/857
        }
        // it.excludeFilter = file("$rootDir/spotbugs-exclude.xml")
    }

}

configure(project(':jmh-benchmark')) {
    apply plugin: 'io.morethan.jmhreport'
    apply plugin: 'me.champeau.gradle.jmh'

    jmh {
        resultFormat = 'JSON'
        
        warmup = '1s' // Time to spend at each warmup iteration.
        warmupIterations = 10
        
        fork = 1
        timeOnIteration = '5s'
        iterations = 5
        benchmarkMode = ['thrpt']
        
        jvmArgs = ['-XX:+UseG1GC']
    }
    
    jmhReport {
        jmhResultPath = project.file("build/reports/jmh/results.json")
        jmhReportOutput = project.file("build/reports/jmh")
    }
    
    tasks.jmh.finalizedBy tasks.jmhReport
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    // Tell idea to output to build/classes/main instead of /out/
    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }

}



